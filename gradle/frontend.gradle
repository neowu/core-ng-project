def yarn(String dir, List<String> params) {
    def isWindows = org.gradle.internal.os.OperatingSystem.current().isWindows()
    def command = isWindows ? ['cmd', '/c', 'yarn'] : ['yarn']
    command.addAll(params)
    return {
        workingDir dir
        commandLine command
    }
}

def env = hasProperty('env') ? env : null

afterEvaluate {
    assert project.ext.has('frontendDir')
    String frontendDir = project.ext['frontendDir']

    tasks.register('buildFrontend') {
        group = 'build'
        doLast {
            assert file(frontendDir).exists()

            exec yarn(frontendDir, ['install'])

            def command = ['run', 'build']
            if (env != null) command.addAll('--env', env)
            exec yarn(frontendDir, command)

            delete 'src/main/dist/web'
            copy {
                from "${frontendDir}/build/dist"
                if (env != null) exclude 'static/*'    // exclude everything in static folder on server env, but keep static folder if exists
                into 'src/main/dist/web'
            }
        }
    }
}

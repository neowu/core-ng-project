buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath "gradle.plugin.com.github.spotbugs:gradlePlugin:1.6.0"
    }
}

subprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: com.github.spotbugs.SpotBugsPlugin
    apply plugin: 'jacoco'

    checkstyle {
        dependencies {
            checkstyle 'com.puppycrawl.tools:checkstyle:8.9'
            checkstyle 'com.github.sevntu-checkstyle:sevntu-checks:1.28.0'
        }

        configFile = rootProject.file('gradle/check/checkstyle.xml')
        configProperties['configDir'] = configFile.parentFile
    }

    pmd {
        ruleSets = []
        ruleSetFiles = rootProject.files('gradle/check/pmd.xml')
        toolVersion = '6.2.0'
        consoleOutput = true
    }

    spotbugs {
        effort = 'max'
        reportLevel = 'low'
        excludeFilter = rootProject.file('gradle/check/spotbugs-filter.xml')
        toolVersion = '3.1.2'
    }

    tasks.withType(com.github.spotbugs.SpotBugsTask) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    jacoco {
        toolVersion = '0.8.0'
    }

    jacocoTestReport {
        // to generate cross sub project report, run: gradlew test && gradlew jacocoTestReport
        executionData = fileTree(rootDir).include('build/**/jacoco/test.exec')

        reports {
            xml.enabled = true
            xml.destination rootProject.file("${buildDir}/reports/jacoco/report.xml")
            html.enabled = true
            csv.enabled = false
        }
    }
}

apply from: file("${rootDir}/gradle/project.gradle")

subprojects {
    group = 'core.framework'
    version = '6.0.1'
}

def mongoVersion = '3.5.0'
def elasticVersion = '6.2.2'
def log4jVersion = '2.9.1'  // used by elasticsearch
def kafkaVersion = '1.0.1'
def jacksonVersion = '2.9.4'
def junitVersion = '5.1.0'

project(':core-ng-api') {
    apply from: file("${rootDir}/gradle/lib.gradle")
}

project(':core-ng') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        api project(":core-ng-api")
        api 'org.slf4j:slf4j-api:1.7.25'
        implementation 'org.javassist:javassist:3.22.0-GA'
        implementation "com.fasterxml.jackson.module:jackson-module-afterburner:${jacksonVersion}"
        implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
        implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jacksonVersion}"
        implementation 'org.apache.httpcomponents:httpclient:4.5.5'
        implementation 'io.undertow:undertow-core:2.0.1.Final'
        compileOnly "org.mongodb:mongo-java-driver:${mongoVersion}"
        compileOnly "org.apache.kafka:kafka-clients:${kafkaVersion}@jar"
        compileOnly "org.elasticsearch.client:transport:${elasticVersion}"
        compileOnly "org.apache.logging.log4j:log4j-api:${log4jVersion}"        // used by elasticsearch
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        testImplementation 'org.mockito:mockito-core:2.15.0'
        testImplementation 'org.assertj:assertj-core:3.9.0'
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
        testRuntimeOnly 'org.hsqldb:hsqldb:2.4.0'
    }
}

project(':core-ng-test') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        api project(":core-ng")
        api "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        api 'org.mockito:mockito-core:2.15.0'
        api 'org.assertj:assertj-core:3.9.0'
        implementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
        compileOnly "org.mongodb:mongo-java-driver:${mongoVersion}"
        compileOnly "com.github.fakemongo:fongo:2.1.0@jar"
        compileOnly "org.apache.kafka:kafka-clients:${kafkaVersion}@jar"
        compileOnly "org.elasticsearch.client:transport:${elasticVersion}"
        compileOnly "org.apache.logging.log4j:log4j-core:${log4jVersion}"       // used by elasticsearch
        testRuntimeOnly 'org.hsqldb:hsqldb:2.4.0'
        testRuntimeOnly 'com.vividsolutions:jts:1.13'                           // used by fongo
        testRuntimeOnly "org.codelibs.elasticsearch.module:lang-painless:${elasticVersion}"
    }
}

def mavenURL = hasProperty('mavenURL') ? mavenURL : null    // usage: "gradlew -PmavenURL=/path clean publish"

configure(subprojects.findAll { it.name.startsWith('core-ng') }) {
    apply plugin: 'maven-publish'
    if (mavenURL != null) {
        assert project.file(mavenURL).exists()
        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java
                    artifact sourcesJar { classifier 'sources' }
                }
            }
            repositories {
                maven { url mavenURL }
            }
        }
    }
}

project(':log-processor') {
    apply from: file("${rootDir}/gradle/app.gradle")
    dependencies {
        compile project(':core-ng')
        compile "org.elasticsearch.client:transport:${elasticVersion}"
        compile "org.apache.kafka:kafka-clients:${kafkaVersion}"
        runtime "org.apache.logging.log4j:log4j-api:${log4jVersion}"            // used by elasticsearch
        testCompile project(':core-ng-test')
        testRuntime "org.apache.logging.log4j:log4j-core:${log4jVersion}"       // used by elasticsearch
        testRuntime "org.codelibs.elasticsearch.module:mapper-extras:${elasticVersion}"     // used by elasticsearch scaled_float
    }
}

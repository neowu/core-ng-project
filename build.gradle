apply from: file("${rootDir}/gradle/project.gradle")

subprojects {
    group = 'core.framework'
    version = '7.6.9'
}

def elasticVersion = '7.10.1'
def kafkaVersion = '2.7.0'
def jacksonVersion = '2.12.0'
def junitVersion = '5.7.0'
def mockitoVersion = '3.7.0'
def assertjVersion = '3.18.1'
def mysqlVersion = '8.0.22'

project('core-ng-api') {
    apply from: file("${rootDir}/gradle/lib.gradle")
}

project('core-ng') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        api project(":core-ng-api")
        api 'org.slf4j:slf4j-api:1.8.0-beta4'
        implementation 'org.javassist:javassist:3.27.0-GA'
        implementation "com.fasterxml.jackson.module:jackson-module-afterburner:${jacksonVersion}"
        implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
        implementation 'com.squareup.okhttp3:okhttp:4.9.0'
        implementation 'io.undertow:undertow-core:2.2.3.Final'
        implementation "org.apache.kafka:kafka-clients:${kafkaVersion}@jar"
        implementation 'org.xerial.snappy:snappy-java:1.1.8.2'      // used by kafka message compression
        compileOnly "mysql:mysql-connector-java:${mysqlVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
        testImplementation "org.assertj:assertj-core:${assertjVersion}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
        testRuntimeOnly 'org.hsqldb:hsqldb:2.5.1'
    }
}

project('core-ng-test') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        api "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        api "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
        api "org.assertj:assertj-core:${assertjVersion}"
        implementation project(":core-ng")
        implementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
        implementation "org.apache.kafka:kafka-clients:${kafkaVersion}@jar"
        testRuntimeOnly 'org.hsqldb:hsqldb:2.5.1'
    }
}

project('core-ng-mongo') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        api project(":core-ng")
        api 'org.mongodb:mongodb-driver-sync:4.1.1'
        testImplementation project(":core-ng-test")
    }
}

project('core-ng-mongo-test') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        implementation project(":core-ng-test")
        implementation project(":core-ng-mongo")
        implementation 'de.bwaldvogel:mongo-java-server:1.36.0'
    }
}

project('core-ng-search') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        api project(":core-ng")
        api "org.elasticsearch:elasticsearch:${elasticVersion}@jar"
        api "org.elasticsearch:elasticsearch-x-content:${elasticVersion}@jar"
        implementation "org.elasticsearch.client:elasticsearch-rest-high-level-client:${elasticVersion}"
        testImplementation project(":core-ng-test")
        implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"              // override transitive jackson dependencies of elasticsearch
    }
}

project('core-ng-search-test') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        implementation project(":core-ng-test")
        implementation project(":core-ng-search")
        implementation "org.elasticsearch.plugin:transport-netty4-client:${elasticVersion}"
        implementation "org.elasticsearch.plugin:reindex-client:${elasticVersion}"
        implementation "org.codelibs.elasticsearch.module:lang-painless:${elasticVersion}"
        implementation "org.codelibs.elasticsearch.module:mapper-extras:${elasticVersion}"      // used by elasticsearch scaled_float
        implementation "org.codelibs.elasticsearch.module:analysis-common:${elasticVersion}"    // used by elasticsearch stemmer
        implementation 'org.apache.logging.log4j:log4j-core:2.11.1'                             // used by elasticsearch local node, must match elasticsearch version
        implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"              // override transitive jackson dependencies of elasticsearch
    }
}

def mavenURL = hasProperty('mavenURL') ? mavenURL : null    // usage: "gradlew -PmavenURL=/path clean publish"

configure(subprojects.findAll { it.name.startsWith('core-ng') }) {
    apply plugin: 'maven-publish'
    if (mavenURL != null) {
        assert project.file(mavenURL).exists()
        publishing {
            publications {
                maven(MavenPublication) {
                    from components.java
                }
            }
            repositories {
                maven { url mavenURL }
            }
        }
    }
}

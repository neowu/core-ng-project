apply from: file("${rootDir}/gradle/project.gradle")

subprojects {
    apply from: file("${rootDir}/gradle/quality.gradle")

    group = 'core.framework'
    version = '3.8.2'
}

def awsVersion = '1.10.27'
def mongoVersion = '3.1.0'
def elasticVersion = '2.0.0'
def rabbitVersion = '3.5.6'

project(':core-ng-api') {
    apply from: file("${rootDir}/gradle/lib.gradle")
}

project(':core-ng') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        provided(
                'redis.clients:jedis:2.7.2@jar',
                "org.elasticsearch:elasticsearch:${elasticVersion}@jar",
                "org.mongodb:mongo-java-driver:${mongoVersion}",
                "com.rabbitmq:amqp-client:${rabbitVersion}",
                "com.amazonaws:aws-java-sdk-core:${awsVersion}@jar",
                "com.amazonaws:aws-java-sdk-sqs:${awsVersion}@jar",
                "com.amazonaws:aws-java-sdk-sns:${awsVersion}@jar"
        )
        compile(
                project(":core-ng-api"),
                'javax.inject:javax.inject:1',
                'org.javassist:javassist:3.20.0-GA',
                'org.slf4j:slf4j-api:1.7.12',
                'com.fasterxml.jackson.module:jackson-module-jaxb-annotations:2.6.3',
                'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.6.3',
                'org.apache.httpcomponents:httpclient:4.5.1',
                'io.undertow:undertow-core:1.3.5.Final'
        )
        testCompile(
                'junit:junit:4.12',
                'org.mockito:mockito-core:1.10.19',
                'org.hsqldb:hsqldb:2.3.3'
        )
    }
}

project(':core-ng-test') {
    apply from: file("${rootDir}/gradle/lib.gradle")
    dependencies {
        provided(
                "org.mongodb:mongo-java-driver:${mongoVersion}",
                'org.hsqldb:hsqldb:2.3.3'
        )
        compile(
                project(":core-ng"),
                'junit:junit:4.12',
                'org.mockito:mockito-core:1.10.19'
        )
    }
}

configure(subprojects.findAll { it.name.startsWith('core-ng') }) {
    apply plugin: 'maven-publish'

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourcesJar {
                    classifier 'sources'
                }
            }
        }

        repositories {
            maven {
                // put following values in gradle.properties, e.g. systemProp.core-ng.repo.s3.url = s3://xxx
                url System.properties['core-ng.repo.s3.url']
                credentials(AwsCredentials) {
                    accessKey System.properties['core-ng.repo.s3.accessKey']
                    secretKey System.properties['core-ng.repo.s3.secretKey']
                }
            }
        }
    }
}

project(':log-processor') {
    apply from: file("${rootDir}/gradle/app.gradle")

    dependencies {
        compile(
                project(':core-ng'),
                "org.elasticsearch:elasticsearch:${elasticVersion}",
                "com.rabbitmq:amqp-client:${rabbitVersion}"
        )
        testCompile(
                project(':core-ng-test')
        )
        testRuntime(
                'org.codehaus.groovy:groovy-all:2.4.4',  // for elastic search script test
        )
    }
}

project(':demo-service') {
    apply from: file("${rootDir}/gradle/app.gradle")

    dependencies {
        compile(
                project(':core-ng')
        )
        testCompile(
                project(':core-ng-test'),
                "org.mongodb:mongo-java-driver:${mongoVersion}"
        )
    }
}

project(':demo-site') {
    apply from: file("${rootDir}/gradle/app.gradle")

    dependencies {
        compile(
                project(':core-ng')
        )
        testCompile(
                project(':core-ng-test')
        )
    }
}